# This is basically Q. Gui's version of hdf5-fortran-cxx without fortran and
# providing hdf5 to make vtk happy.

pkgname=ros-hdf5-cxx
_pkgname=hdf5
pkgver=1.8.8
_pkgver=1.8.8
pkgrel=2
arch=('i686' 'x86_64')
pkgdesc="General purpose library and file format for storing scientific data with GNU C++ support enabled, but without threadsafe."
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
conflicts=('hdf5')
provides=('hdf5')
depends=('zlib' 'sh'  'gcc')
makedepends=('time')
source=(ftp://ftp.hdfgroup.org/HDF5/current/src/${_pkgname}-${pkgver}.tar.bz2)
options=('!libtool')
sha1sums=('1bc16883ecd631840b70857bea637a06eb0155da')

build() {
  cd $srcdir/${_pkgname}-${_pkgver}
  export CFLAGS="${CFLAGS/O2/O0}"
  export CXXFLAGS="${CFLAGS}"
  ./configure --prefix=/usr \
      FC=gfortran  \
      CXX=g++ \
    --enable-cxx \
    --enable-hl \
    --enable-linux-lfs \
    --enable-production \
    --with-pic \
    --docdir=/usr/share/doc/hdf5/ \
    --disable-sharedlib-rpath
  make
}

package() {
  cd $srcdir/${_pkgname}-${_pkgver}

  make -j1 DESTDIR=${pkgdir} install

  install -d -m755 $pkgdir/usr/share/licenses/${_pkgname}
  install -m644 $srcdir/${_pkgname}-${_pkgver}/COPYING \
          $pkgdir/usr/share/licenses/${_pkgname}/LICENSE 
}


